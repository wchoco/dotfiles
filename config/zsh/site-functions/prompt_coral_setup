prompt_coral_help () {
  cat <<EOH
  prompt coral [<text-color> [<parentheses-color>]]
EOH
}

prompt_coral_setup () {
  local text_col=${1:-'cyan'}
  local parens_col=${2:-'magenta'}

  local text="%b%F{$text_col}"
  local parens="%b%F{$parens_col}"
  local punct="%b%F{$parens_col}"
  local reset="%b%f"

  local lpar="$parens [$text"
  local rpar="$parens]$text"

  PS1="$punct┌ $text%n$punct@$text%m$lpar$text%D{%H:%M:%S}$rpar$punct$text %#:%~$prompt_newline$punct│ $reset"

  PS2="$parens$text$punct│ $reset"

  prompt_opts=(cr subst percent)
}

prompt_coral_preview () {
  local color colors
  colors=(red yellow green blue magenta)

  if (( ! $#* )); then
    for (( i = 1; i <= $#colors; i++ )); do
      color=$colors[$i]
      prompt_preview_theme coral $color
      (( i < $#colors )) && print
    done
  else
    prompt_preview_theme coral "$@"
  fi
}

prompt_coral_setup "$@"
